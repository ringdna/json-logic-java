plugins {
  id "java"
  id "maven"
  id "signing"
}

group "com.brunocesar"
version "1.0.0-SNAPSHOT"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenCentral()
}

dependencies {
  compile "com.google.guava:guava:20.0"
  compile "com.google.code.gson:gson:2.8.5"
  testCompile "junit:junit:4.12"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = "javadoc"
  from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
  classifier = "sources"
  from sourceSets.main.allSource
}

artifacts {
  archives jar, javadocJar, sourcesJar
}

if (project.hasProperty('release')) {
  signing {
    useInMemoryPgpKeys(signingKey, signingKeyPassword)
    sign configurations.archives
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      if (project.hasProperty('release')) {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }
      }

      pom.project {
        name "json-logic-java"
        artifactId "json-logic-java"
        packaging "jar"
        description "A native Java implementation of the json-logic project"
        url "https://github.com/brunocesarsilva/json-logic-java"

        scm {
          connection "scm:git@github.com:brunocesarsilva/json-logic-java.git"
          developerConnection "scm:git@github.com:brunocesarsilva/json-logic-java.git"
          url "https://github.com/brunocesarsilva/json-logic-java.git"
        }

        licenses {
          license {
            name "MIT License"
            url "https://github.com/brunocesarsilva/json-logic-java/blob/master/LICENSE"
          }
        }

        developers {
          developer {
            id "jamsesso"
            name "Sam Jesso"
            email "samuel.jesso@gmail.com"
          }
          developer {
            id "brunocesarsilva"
            name "Bruno Ribeiro"
            email "bruno@brunocesar.com"
          }
        }
      }
    }
  }
}
